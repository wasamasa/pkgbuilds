pkgname=ui-git
pkgver=alpha3.1.r159.gec15e25
pkgrel=1
pkgdesc="A portable GUI library for C"
arch=('i686' 'x86_64')
url="https://github.com/andlabs/libui"
license=('MIT')
depends=('gtk3')
makedepends=('cmake' 'git')

provides=('ui')
conflicts=('ui')

source=("git+https://github.com/andlabs/libui.git")
md5sums=('SKIP')

pkgver() {
  cd libui
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd libui
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RelWithDebInfo
  make
}

package() {
  cd libui
  # https://github.com/andlabs/libui/issues/116
  # make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir"/usr/lib
  install -m644 out/libui.so.0 "$pkgdir"/usr/lib
  ln -sf /usr/lib/libui.so.0 "$pkgdir"/usr/lib/libui.so
  install -m755 -d "$pkgdir"/usr/include
  install -m644 ui.h "$pkgdir"/usr/include
  install -m644 ui_unix.h "$pkgdir"/usr/include
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
