pkgname=kawa-git
pkgver=2.93.r106.gb837eb898
pkgrel=1
pkgdesc='A Scheme implementation and framework targeted the JVM.'
arch=('i686' 'x86_64')
url='http://www.gnu.org/software/kawa/index.html'
license=('X11/MIT')
depends=('java-environment' 'java-jline3')
source=("git+https://gitlab.com/kashell/Kawa.git")
sha256sums=('SKIP')
conflicts=('kawa')
provides=('kawa')
replaces=('kawa')

pkgver() {
  cd Kawa
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd Kawa
  ./autogen.sh
  ./configure --with-jline3=/usr/share/java/jline3.jar --prefix=/usr
  make -j1
}

package() {
  cd Kawa
  make -j1 DESTDIR="$pkgdir" install
}
